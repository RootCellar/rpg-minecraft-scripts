# Evil script

Options:
	prefix: &8[&7Evil Skript&8]&r
	consolePrefix: &6[&8Console&6]&r

	debugPrefix: &e[&bDEBUG&e]&r
	debug: true

on script load:
	evilScriptBroadcast("Initializing script...")

	evilScriptConsoleMessage("Test console message")
	evilScriptDebugBroadcast("Debug Output Enabled")
	evilScriptDebugConsoleMessage("Test debug to console")

	# Initialize variables and such as needed
	set {_verified} to verifySpecialCreatureTypes()
	if {_verified} is not true:
		evilScriptBroadcast("Creatures failed validation!")

	evilScriptBroadcast("Script Loaded!")

on script unload:
	evilScriptBroadcast("Cleaning up...")

	# Clean up variables and entities and such as needed

	evilScriptBroadcast("Unloading script...")

local function evilLog(message: text):
	log "%{_message}%" to "evil_script/main.log"

local function evilScriptBroadcast(message: text):
	broadcast "{@prefix} &3%{_message}%"

local function evilScriptConsoleMessage(message: text):
	send "{@consolePrefix} {@prefix} &3%{_message}%" to console

local function shouldPrintDebugType(type: string) :: boolean:
	if {evilSkript::debug::%{_type}%} is not set:
		set {evilSkript::debug::%{_type}%} to false
	return {evilSkript::debug::%{_type}%}

local function evilScriptDebugBroadcast(message: text):
	evilScriptDebugBroadcastType("main", {_message})

local function evilScriptDebugBroadcastType(type: string, message: text):
	{@debug} is true:
		shouldPrintDebugType({_type}) is true:
			broadcast "{@debugPrefix} {@prefix} &f%{_type}% &7%{_message}%"

local function evilScriptDebugConsoleMessage(message: text):
	evilScriptDebugConsoleMessageType("main", {_message})

local function evilScriptDebugConsoleMessageType(type: string, message: text):
	{@debug} is true:
		shouldPrintDebugType({_type}) is true:
			send "{@consolePrefix} {@debugPrefix} {@prefix} &f%{_type}% &7%{_message}%" to console

command /evildebug <string> <boolean>:
	trigger:
		command executor has permission "evil.control"
		set {evilSkript::debug::%arg 1%} to arg 2
		send "&3Debug logging changed" to command executor
		evilScriptConsoleMessage("Debug logging changed: %arg 1% -> %arg 2%")

command /evildebugged <integer=1>:
	trigger:
		command executor has permission "evil.control"

		loop {evilSkript::debug::*}:
			add "&3%loop-index% -> %loop-value%" to {_debuggedLines::*}

		set {_lines::*} to paginate({_debuggedLines::*}, arg 1)

		loop {_lines::*}:
			send loop-value to command executor

command /evildebugreset:
	trigger:
		command executor has permission "evil.control"
		delete {evilSkript::debug::*}
		send "&cEnabled debug types reset" to command executor
		evilScriptConsoleMessage("Enabled debug types reset")

local function isValidEntityType(name: string) :: boolean:
	set {_entity} to "%{_name}%" parsed as entity type
	{_entity} is an entity:
		evilScriptDebugConsoleMessageType("entityType", "'%{_name}%' is a valid entity")
		return true
	else:
		evilScriptDebugConsoleMessageType("entityType", "'%{_name}%' is not a recognized entity")
		return false

local function listBosses() :: texts:
	add "test" to {_bosses::*}

	return {_bosses::*}

local function getMobTypeForBoss(name: text) :: text:
	return {_none}

local function listSpecials() :: texts:
	add "test" to {_specials::*}

	return {_specials::*}

local function getMobTypeForSpecial(name: text) :: text:
	return {_none}

local function verifySpecialCreatureTypes() :: boolean:
	set {_toRet} to true

	loop listBosses():
		if isValidEntityType(getMobTypeForBoss(loop-value)) is false:
			evilScriptDebugConsoleMessageType("boss", "Boss '%loop-value%' failed mob type validation!")
			set {_toRet} to false

	loop listSpecials():
		if isValidEntityType(getMobTypeForSpecial(loop-value)) is false:
			evilScriptDebugConsoleMessageType("special", "Special '%loop-value%' failed mob type validation!")
			set {_toRet} to false

	return {_toRet}

on spawn:
	spawn reason is natural
	entity is a monster
	entity is a zombie:
		entity is not a baby zombie
		chance of 35%:
			makeSpecial(entity)
	else if entity is a skeleton:
		chance of 35%:
			makeSpecial(entity)
	else if entity is a spider:
		chance of 25%:
			makeSpecial(entity)

function makeSpecial(entity: entity):
	{_entity} is a zombie:
		{_entity} is not a baby zombie
		#set name of {_entity} to "&cHerobrine's Chosen"
		set attack damage attribute of {_entity} to 1.8 * attack damage attribute of {_entity}
		set movement speed attribute of {_entity} to 1.2 * movement speed attribute of {_entity}
		set maximum health of {_entity} to 2.2 * maximum health of {_entity}
		heal {_entity}
		equip {_entity} with iron helmet
		equip {_entity} with iron chestplate
		equip {_entity} with iron leggings
		equip {_entity} with iron boots
		set {_entity}'s held item to iron sword
		loop 2 times:
			spawn a skeleton at {_entity}
	else if {_entity} is a skeleton:
		#set name of {_entity} to "&cHerobrine's Chosen"
		set attack damage attribute of {_entity} to 2.0 * attack damage attribute of {_entity}
		set movement speed attribute of {_entity} to 1.3 * movement speed attribute of {_entity}
		set maximum health of {_entity} to 1.7 * maximum health of {_entity}
		equip {_entity} with chain helmet
		equip {_entity} with chain chestplate
		equip {_entity} with chain leggings
		equip {_entity} with chain boots
		set {_entity}'s held item to iron sword
		loop 2 times:
			spawn a zombie at {_entity}
	else if {_entity} is a spider:
		loop 3 times:
			spawn a cave spider at {_entity}
