
Options:
	prefix: &d[&7Debug Skript&d]&r
	broadcastPrefix: &7[&dBroadcast&7]&r
	consolePrefix: &7[&dConsole&7]&r

	debugPrefix: &e[&bDEBUG&e]&r
	debug: true

on script load:
	scriptBroadcast("Loaded!")
	scriptConsoleMessage("Loaded!")

on script unload:
	scriptBroadcast("Unloading...")
	scriptConsoleMessage("Unloading...")

local function scriptBroadcast(message: text):
	broadcast "{@broadcastPrefix} {@prefix} &3%{_message}%"

local function scriptConsoleMessage(message: text):
	send "{@consolePrefix} {@prefix} &3%{_message}%" to console

local function shouldPrintDebugType(type: string) :: boolean:
	if {debugSkript::debug::%{_type}%} is not set:
		set {debugSkript::debug::%{_type}%} to false
	return {debugSkript::debug::%{_type}%}

function debugScriptDebugBroadcast(message: text):
	debugScriptDebugBroadcastType("main", {_message})

function debugScriptDebugBroadcastType(type: string, message: text):
	{@debug} is true:
		shouldPrintDebugType({_type}) is true:
			#broadcast "{@debugPrefix} {@prefix} &f%{_type}% &7%{_message}%"
			scriptBroadcast("&f%{_type}% &7%{_message}%")

local function debugScriptDebugConsoleMessage(message: text):
	debugScriptDebugConsoleMessageType("main", {_message})

function debugScriptDebugConsoleMessageType(type: string, message: text):
	{@debug} is true:
		shouldPrintDebugType({_type}) is true:
			#send "{@consolePrefix} {@debugPrefix} {@prefix} &f%{_type}% &7%{_message}%" to console
			scriptConsoleMessage("&f%{_type}% &7%{_message}%")

command /skriptdebug <string> <boolean>:
	trigger:
		command executor has permission "rpg.control"
		set {debugSkript::debug::%arg 1%} to arg 2
		send "&3Debug logging changed" to command executor
		scriptConsoleMessage("Debug logging changed: %arg 1% -> %arg 2%")

command /skriptdebugged <integer=1>:
	trigger:
		command executor has permission "rpg.control"

		loop {debugSkript::debug::*}:
			add "&3%loop-index% -> %loop-value%" to {_debuggedLines::*}

		set {_lines::*} to paginate({_debuggedLines::*}, arg 1)

		loop {_lines::*}:
			send loop-value to command executor

command /skriptdebugreset:
	trigger:
		command executor has permission "rpg.control"
		delete {debugSkript::debug::*}
		send "&cEnabled debug types reset" to command executor
		scriptConsoleMessage("Enabled debug types reset")
