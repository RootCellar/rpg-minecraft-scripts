Options:
	prefix: &b[&aChat Fun Skript&b]&r
	consolePrefix: &6[&8Console&6]&r

on script load:
	consoleMessage("Loaded!")

on script unload:
	consoleMessage("Unloading...")

local function consoleMessage(message: text):
	send "{@consolePrefix} {@prefix} &e%{_message}%" to console

local function broadcastMessage(message: text):
	broadcast "{@prefix} &e%{_message}%"

local function fakeJoinName(name: text):
	broadcast "&e%{_name}% joined the game"

local function fakeLeaveName(name: text):
	broadcast "&e%{_name}% left the game"

local function godSay(line: text):
	broadcast "&a[&cGod&a] %{_line}%"

command /godsay <text>:
	trigger:
		command executor has permission "chatfun.god"
		godSay(arg 1)

local function botSay(line: text, extra: boolean=true):
	broadcast "&7[&9Bot&7] &c%{_line}%"

	{_extra} is true
	chance of 10%:
		wait 30 ticks
		skripterSay("Hmm...seems buggy")
		wait 20 ticks
		fakeLeaveName("Bot")

command /botsay <text>:
	trigger:
		command executor has permission "chatfun.bot"
		botSay(arg 1, false)

local function skripterSay(line: text):
	broadcast "&c[&7Skripter&c] &f%{_line}%"

command /skriptersay <text>:
	trigger:
		command executor has permission "chatfun.skripter"
		skripterSay(arg 1)

local function linuxUserSay(line: text):
	broadcast "&3[&7Linux User&3] &b%{_line}%"

command /linuxusersay <text>:
	trigger:
		command executor has permission "chatfun.linuxuser"
		linuxUserSay(arg 1)

command /fakejoin <text>:
	trigger:
		command executor has permission "chatfun.fake"
		fakeJoinName(arg 1)

command /fakeleave <text>:
	trigger:
		command executor has permission "chatfun.fake"
		fakeLeaveName(arg 1)

on chat:
	wait 5 ticks
	the message contains "god":
		chance of 30%:
			fakeJoinName("God")
			wait 10 ticks
		godSay("Huh?")

on join:
	wait 50 ticks
	godSay("Hey, %event-player%! Welcome to the game!")
	wait 35 ticks
	chance of 40%:
		wait 5 ticks
		botSay("Welcome, %event-player%!")
	chance of 10%:
		wait 5 ticks
		skripterSay("Howdy, %event-player%!")
	chance of 10%:
		wait 5 ticks
		linuxUserSay("Hey, %event-player%!")

on quit:
	wait 50 ticks
	godSay("Bye, %event-player%!")

on death:
	wait 5 ticks
	victim is a player:
		godSay("RIP")
	victim is not a player:
		victim is a zombie:
			chance of 2%:
				godSay("You should've seen what that zombie did while he was alive")
		victim is a chicken:
			attacker is a player
			chance of 2%:
				godSay("Who's *really* the chicken here?")

on target:
	target is a player
	chance of 5%:
		godSay("Watch out, %target%!")

