Options:
	prefix: &d[&aGeneral Script&d]&r
	consolePrefix: &6[&8Console&6]&r

on script load:
	consoleMessage("Loaded!")

on script unload:
	consoleMessage("Unloading...")

local function consoleMessage(message: text):
	send "{@consolePrefix} {@prefix} &e%{_message}%" to console

command /cbcast <boolean>:
	trigger:
		command executor is console
		set {console.broadcast} to argument
		message "&cSet console broadcast mode to %argument%"
		
on command:
	command executor is console
	{console.broadcast} is true
	broadcast "&7Server Console executing command: /%command% %concatStringList(arguments)%"
	send "&4Warning: &cConsole Broadcasting is on!" to console

command /feedback [<text="">]:
	trigger:
		argument is "":
			send "&bSend your feedback or ideas for this server, which will be stored in a file." to player
			stop
		log "[%command executor%] %argument%" to "feedback/feedback.log"
		send "&bFeedback filed."
		send "&b[FeedBack] &2%command executor%: &c%argument%" to console

on join:
	wait 5 seconds
	send "&bUse '/feedback' to send feedback or ideas to be filed and viewed by the server administrator" to the player

local function concatStringList(list: texts, delim: string=" ") :: text:
	set {_toRet} to ""
	set {_first} to true

	loop {_list::*}:
		if {_first} is true:
			set {_toRet} to "%loop-value%"
			set {_first} to false
		else:
			set {_toRet} to "%{_toRet}%%{_delim}%%loop-value%"

	return {_toRet}

